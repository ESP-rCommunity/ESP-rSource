C This file is part of the ESP-r system.
C Copyright Energy Systems Research Unit, University of
C Strathclyde, Glasgow Scotland, 2001.

C ESP-r is free software.  You can redistribute it and/or
C modify it under the terms of the GNU General Public
C License as published by the Free Software Foundation 
C (version 2 orlater).

C ESP-r is distributed in the hope that it will be useful
C but WITHOUT ANY WARRANTY; without even the implied
C warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
C PURPOSE. See the GNU General Public License for more
C details.

C You should have received a copy of the GNU General Public
C License along with ESP-r. If not, write to the Free
C Software Foundation, Inc., 59 Temple Place, Suite 330,
C Boston, MA 02111-1307 USA.

C This file contains the following routines:
C  MOTABL Produces tabular output for a 24 hour period.
C  ZONTAB Multi-column tabular listing for performance metrics.
C  MGLTBL monthly multi-column tabular listing for selected gains & losses.
C  CZMRT  returns one day's temperatures of zone mean radiant temp.
C  CZRESL returns one day's temperatures of zone resultant temp.

C ******************** MOTABL ********************
C MOTABL produces tabular output for a 24 hour period.
      SUBROUTINE MOTABL
#include "building.h"
#include "net_flow.h"
C #include "esprdbfile.h"
C esprdbfile.h supplies the following:
C default file names for databases
C      COMMON/SPAD/MMOD,LIMIT,LIMTTY
      COMMON/OUTIN/IUOUT,IUIN
C      COMMON/FILEP/IFIL
C      COMMON/OUTPCH/ICOUT

      COMMON/SIMPIK/ISIM,ISTADD,ID1,IM1,ID2,IM2,ISDS,ISDF,NTS,ISAVE
C      COMMON/PERO/IOD1,IOM1,IOH1,IOD2,IOM2,IOH2,IODS,IODF,NOUT,IAV
C      COMMON/ZONPIK/NZ,NZNO(MCOM)

C ihflag = 0 write 13h30, ihflag = 1 write 0.5625, = 2 write yyyy-mm-dd 13:30:00
C idhflg = 0 no day demarcations, idhflg = 1 write demarcation
C          between tabular reporting days.
C ilflag = 0 tabular labels on multi-lines, ilflag = 1 on one line.
      COMMON/GRTOOL/IHFLAG,IDHFLG,ILFLAG
      common/exporttg/xfile,tg,delim
      COMMON/EXPORTI/ixopen,ixunit,ixpunit
C      common/prec7/itcnst

C Special materials file flag
C      common/spmfxst/ispmxist,spflnam  
C      common/spmatl/nspmnod,ispmloc(mspmnod,3),ispmtyp(mspmnod,2),
C     &nnodat(mspmnod),spmdat(mspmnod,mspmdat) 
C      COMMON/MFLCTL/IRY,IRM,IRD,IRH,FLWTIM,IHOUR,IYD,IFYD,ILYD,IPROG 
      common/getmenu/menutype,igetind(65),igetflux(65)

      CHARACTER*23 ITEM(15)
      character xfile*144,tg*1,delim*1
C      character spflnam*72
      integer IWM  ! for radio button
      integer NITMS,INO ! max items and current menu item

C For help messages
      character helpinsub*24   ! subroutine name
      character helptopic*24 ! string (unique) for topic
      integer nbhelp     ! number of help lines found

      helpinsub='MOTABL'  ! set for subroutine

C Default value for GRTOOL file = 0 (off), day hash marks off.
      IHFLAG=0
      IDHFLG=0
      ILFLAG=0

  503 ITEM(1)   ='2 select result set    '
      ITEM(2)   ='3 define period        '
      ITEM(3)   ='4 select zones         '
      ITEM(4)   ='  ___________________  '
      ITEM(5)   ='g performance metrics  '
      ITEM(6)   ='h special material data'
      ITEM(7)   ='i network air/wtr flow '
      ITEM(8)   ='  ___________________  '
      ITEM(9)   ='  formatting...        '
      if(ixopen.eq.1)then
        ITEM(10)='> output >> file       '
      elseif(ixopen.eq.0)then
        ITEM(10)='> output >> screen     '
      endif
      if(IHFLAG.eq.0)then
        ITEM(11)='* time >> 10h30        '
      elseif(IHFLAG.eq.1)then
        ITEM(11)='* time >> 0.4375       '
      elseif(IHFLAG.eq.2)then
        ITEM(11)='* time >>mm-dd 10:30:00'
      endif
      if(ILFLAG.eq.0)then
        ITEM(12)='& labels >> multiline  '
      elseif(ILFLAG.eq.1)then
        ITEM(12)='& labels >> on one line'
      endif
      if(delim.eq.'-')then
        ITEM(13)='^ delim >> normal      '
      elseif(delim.eq.'T')then
        ITEM(13)='^ delim >> TAB         '
      elseif(delim.eq.'C')then
        ITEM(13)='^ delim >> comma       '
      elseif(delim.eq.'S')then
        ITEM(13)='^ delim >> space       '
      elseif(delim.eq.'X')then
        ITEM(13)='^ delim >> tagged      '
      endif
      ITEM(14)  ='? help                 '
      ITEM(15)  ='- exit                 '
      NITMS=15
      INO=-2

C Instanciate help string array for all interactions.
      helptopic='res_tabular_menu'
      call gethelptext(helpinsub,helptopic,nbhelp)

  504 CALL EMENU('Tabular Output',ITEM,NITMS,INO)

C Test for illegal menu pick.
      IF(INO.EQ.1)then
        CALL MORESS
      elseif(INO.EQ.2)then
        CALL MOOPER
      elseif(INO.EQ.3)then
        CALL MOZDFN
      elseif(INO.EQ.5)then
        if(ISAVE.GT.1)then
          CALL ZONTAB('-')
        else
          call edisp(iuout,'Not available with save level 1')
        endif
      elseif(INO.EQ.6)then

C Special material output. << use alternative data recovery >>.

      elseif(INO.EQ.7)then

C Network flow tabular output.
        MENUTYPE=4
        call MFOUTP('-')
      elseif(INO.EQ.10.AND.ISAVE.GE.1)then
        call ctlexp(xfile,ixopen,ixunit,ixpunit,'X','Tabular',IER)
      elseif(INO.EQ.11.AND.ISAVE.GE.1)then

C Toggle time format and day separators.
        IHFLAG=IHFLAG+1
        if(IHFLAG.GT.2)IHFLAG=0
        if(IHFLAG.eq.0)then
          call edisp(iuout,' ')
          call edisp(iuout,'setting standard display time = 10h30')
        elseif(IHFLAG.eq.1)then
          call edisp(iuout,' ')
          call edisp(iuout,'setting time = day.fraction 10.5000')
        elseif(IHFLAG.eq.2)then
          call edisp(iuout,' ')
          call edisp(iuout,'setting time = yyyy-mm-dd hh:mm:ss')
        endif
        CALL EASKAB('Include mark between days when ',
     &              'displaying or writing data :','no','yes',
     &              IDH,nbhelp)
        IDHFLG=IDH-1
      elseif(INO.EQ.12.AND.ISAVE.GE.1)then

C Toggle tabular listing label format.
        ILFLAG=ILFLAG+1
        if(ILFLAG.GT.1)ILFLAG=0
        if(ILFLAG.eq.0)then
          call edisp(iuout,' ')
          call edisp(iuout,'column labels on multi-lines ')
        elseif(ILFLAG.eq.1)then
          call edisp(iuout,' ')
          call edisp(iuout,'column labels on one line (spreadsheets)')
        endif
      elseif(INO.EQ.13.AND.ISAVE.GE.1)then

C Toggle delimeter.
        IWM=1
        CALL EASKATOG('Delimeter to use between columns of data:',' ',
     &    'normal spaces','single space','tab','comma','tagged',
     &    'continue',' ',IWM,nbhelp)
        if(iwm.eq.1)then
          delim = '-'
        elseif(iwm.eq.2)then
          delim = 'S'
        elseif(iwm.eq.3)then
          delim = 'T'
        elseif(iwm.eq.4)then
          delim = 'C'
        elseif(iwm.eq.5)then
          delim = 'X'
        endif
      elseif(INO.EQ.14)then
        helptopic='res_tabular_menu'
        call gethelptext(helpinsub,helptopic,nbhelp)
        CALL PHELPD('tabular',nbhelp,'-',0,0,IER)
      elseif(INO.GE.15)then
        RETURN
      else

C Output menu error signal and allow reselection from menu.
       INO=-1
        goto 504
      endif
      goto 503

      END

C ******************** ZONTAB
C ZONTAB A multi column tabular listing for performance metrics
C such as found in TGRAPH and STATS.
C act='p' iget parameters already set.
C act='m' called from ipv metric with parameters already set.

      SUBROUTINE ZONTAB(act)
#include "building.h"
#include "ipvdata.h"
      
      integer lnblnk  ! function definition

      COMMON/OUTPCH/ICOUT
C      COMMON/SPAD/MMOD,LIMIT,LIMTTY

      COMMON/SET1/IYEAR,IBDOY,IEDOY,IFDAY,IFTIME
      COMMON/SIMPIK/ISIM,ISTADD,ID1,IM1,ID2,IM2,ISDS,ISDF,NTS,ISAVE
      COMMON/PERO/IOD1,IOM1,IOH1,IOD2,IOM2,IOH2,IODS,IODF,NOUT,IAV

      COMMON/IGETFLG/IOCUPF,ialstused,IROC
      COMMON/GETPIK/NGET,IGETNO(MZS,9)
      common/getmenu/menutype,igetind(65),igetflux(65)

      character SLABEL*32,GLABEL*20,TABLABEL*36
      COMMON/GETLABEL/SLABEL(MZS),GLABEL(MZS),TABLABEL(MZS)
      integer LNSLABEL,LNGLABEL,LNTABLABEL  ! lengths for label strings
      COMMON/LNGETLABEL/LNSLABEL(MZS),LNGLABEL(MZS),LNTABLABEL(MZS)
      COMMON/GET1/VAL1(MZS,MTS),VAL2(MZS,MTS),VAL3(MZRL,MTS)

      COMMON/RESLIB/RFILE,PFILE,MSTRFILE,LAFRES
      COMMON/SETNAM/RSNAME(MNRS)
      COMMON/SIMPKA/NSIM

      COMMON/GRTOOL/IHFLAG,IDHFLG,ILFLAG
      common/exporttg/xfile,tg,delim
      COMMON/EXPORTI/ixopen,ixunit,ixpunit

      integer ireportunit ! zero is default one is W two is kW three Joules
      common/repunit/ireportunit

      character outs*124,outs248*248 ! string buffers
      character outs800*800,outs800d*800  ! really long string buffers
      character act*1,sq*1,unit*7,t28*28,dg*1
      character LTIME*5,LJTIME*8
      character prompt*124,tg*1,delim*1,xfile*144
      CHARACTER PDESCR*60,SDESCR*44,RSNAME*40,DESCR*7,DESCR1*10
      character rfile*72,PFILE*72,MSTRFILE*72,LAFRES*72
      integer lnb    ! length of buffer to write to
      integer K,K4   ! counters
      integer KE,KE4 ! current end points

C If output to file alter the edisp unit number.
      itru = icout
      if(ixopen.eq.1)then
        itru = ixunit
        if(NGET.ge.1)then
          write(prompt,'(a,a)') SLABEL(1)(1:LNSLABEL(1)),' >> file.'
        else
          write(prompt,'(a)')' Output being directed to file. ' 
        endif
        call edisp(icout,prompt)
      endif

C Make up a single quote for IPV
      sq = char(39)

C Call the menu of choices (this also sets some default options).
 1    if(act(1:1).eq.'-')then
        MENUTYPE=4
        call GOMSETUP
        call GOMENU
        if(ixopen.eq.1)then
          itru = ixunit
          if(NGET.ge.1)then
            write(prompt,'(a,a)') SLABEL(1)(1:LNSLABEL(1)),' >> file.'
          else
            write(prompt,'(a)')' Output being directed to file. ' 
          endif
          call edisp(icout,prompt)
        else
          itru = icout
        endif
        if (MENUTYPE.eq.-1) return
      endif
      IINLVDT=0

C Call GOGET for each output day to get required data.
C GOGET recovers the data in VAL2 (and averages output if required.)
C Set start and finish times.
C TSTART and TFINISH - start and finish times in hours from IOH1 on the 
C first day of output.
      TSTART=FLOAT(IOH1)
      TFINSH=FLOAT(((IODF)*24+IOH2)-(IODS)*24)

C NDTS - the number of timesteps in a day.
      NDTS=24*NTS

C Write tag line if tagged output requested.
      if (delim.eq.'X') then
        write(outs,'(a)')'*table'
        call edisp(itru,outs)
        write(outs,'(a)')'*col 1 time'
        call eddisp(itru,outs)
        do 15 IL=1,NGET
          write(outs,'(a,6i3)')'*col ',IL+1,(IGETNO(IL,ILL),ILL=1,5)
          call eddisp(itru,outs)
 15     continue
      endif
      
C Write general header information (skip if IPV mode).
      if(act(1:1).ne.'m')then
        CALL HDDATE(PDESCR)
        CALL HDSTEP(SDESCR)
        
C If julian or 13:30:00 requested or writing to file start lines with a '#'.
        if (IHFLAG.eq.1.or.IHFLAG.eq.2.or.ixopen.eq.1) then
          call edisp(itru,'# Timestep performance metrics.')
          lsn1=MIN0(lnblnk(RFILE),42)
          if(NSIM.gt.1)then
            write(outs248,'(3A,I4,2A)')'# Lib: ',RFILE(1:lsn1),' Set:',
     &      ISIM,': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
          else
            write(outs248,'(4A)')'# Lib: ',RFILE(1:lsn1),
     &        ': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
          endif
          if(ixopen.eq.1)then
            lnb=lnblnk(outs248)   ! write directly to file
            write(itru,'(A)',iostat=ios,err=1)outs248(1:lnb)
          else
            lnb=MIN0(lnblnk(outs248),124)
            if(lnb.lt.124)then
              call edisp(itru,outs248(1:lnb))
            else
              call edisp248(itru,outs248,124)
            endif
          endif
          WRITE(outs,'(4A)')'# ',PDESCR(1:lnblnk(PDESCR)),' ',
     &      SDESCR(11:lnblnk(SDESCR))
          call edisp(itru,outs)
        else

C Writing to the screen. It would be great to figure out how wide
C we can write to << ?? to be done >>
          call edisp(itru,'Timestep performance metrics.')
          lsn1=MIN0(lnblnk(RFILE),42)
          if(NSIM.gt.1)then
            write(outs248,'(3A,I4,2A)')'Lib: ',RFILE(1:lsn1),' Set:',
     &        ISIM,': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
          else
            write(outs248,'(4A)')'Lib: ',RFILE(1:lsn1),
     &        ': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
          endif
          if(ixopen.eq.1)then
            lnb=lnblnk(outs248)   ! write directly to file
            write(itru,'(A)',iostat=ios,err=1)outs248(1:lnb)
          else
            lnb=MIN0(lnblnk(outs248),124)
            if(lnb.lt.124)then
              call edisp(itru,outs248(1:lnb))
            else
              call edisp248(itru,outs248,124)
            endif
          endif

          WRITE(outs,'(3A)')PDESCR(1:lnblnk(PDESCR)),' ',
     &      SDESCR(11:lnblnk(SDESCR))
          call edisp(itru,outs)
        endif
      endif

      do 10 IDAY=IODS,IODF

C Write day of results and column headings information (skip if IPV mode).
        CALL STDATE(IYEAR,IDAY,DESCR,DESCR1)
        if(act(1:1).ne.'m')then
          if (IDHFLG.eq.1.and.ixopen.eq.1) then
            outs='# '
            call edisp(itru,outs)
            write(outs,'(3A)')'# ',DESCR1,' timestep misc. listing'
            call edisp(itru,outs)
            outs='# '
            call edisp(itru,outs)
          elseif (IDHFLG.eq.0.and.ixopen.eq.1) then
            continue
          elseif (IDHFLG.eq.0.and.ixopen.eq.0) then
            continue
          elseif (IDHFLG.eq.1.and.ixopen.eq.0) then
            outs='  '
            call edisp(itru,outs)
            write(outs,'(3A)')' ',DESCR1,' timestep misc. listing'
            call edisp(itru,outs)
          else
            outs='  '
            call edisp(itru,outs)
          endif
          
C If printing to screen then split column headings over four rows 
C   don't do this if printing to file.
          irows=4
          if (ixopen.eq.1) irows=1
          is=1
          do 100 ir=1,irows
            K=6   ! initial value for 248 buffer
            K4=6  ! initial value for 800 buffer
            if (ir.gt.1) then
              outs248='     '  ! clear for 248 buffer
              outs800='     '  ! clear for 800 buffer
            else
              if ((IHFLAG.eq.1.or.IHFLAG.eq.2).and.ixopen.eq.1) then
                if (delim.eq.'X') then
                  outs248='*time'
                  outs800='*time'
                else
                  outs248='#Time'
                  outs800='#Time'
                endif
              elseif ((IHFLAG.eq.1.or.IHFLAG.eq.2).and.ixopen.eq.0)then
                if (delim.eq.'X') then
                  outs248='*time'
                  outs800='*time'
                else
                  outs248='Time'
                  outs800='Time'
                endif
              else
                outs248='Time '
                outs800='Time '
              endif
            endif
            do 20 IL=1,NGET
            
C Define units. Note: climate data will base its units on 
C the IGETNO(IL,8) value along with all other items. Take
C into account the value of ireportunit.
              if (IGETNO(IL,8).eq.1) then
                unit='(degC)'
              elseif (IGETNO(IL,8).eq.2) then
                if(IGETNO(IL,1).eq.8.or.IGETNO(IL,1).eq.9.or.
     &             IGETNO(IL,1).eq.10.or.IGETNO(IL,1).eq.30.or.
     &             IGETNO(IL,1).eq.31.or.IGETNO(IL,1).eq.32.or.
     &             IGETNO(IL,1).eq.33) then
                  if(ireportunit.eq.0.or.ireportunit.eq.2)then
                    unit='(kW)'
                  else
                    unit='(W)'
                  endif
                else
                  unit='(W)'
                endif
              elseif (IGETNO(IL,8).eq.3) then
                unit='(W/m^2)'
              else
                unit='(-)'
              endif
              
C Set end character for output string (KE or KE4) and for section of 
C  tablabel (ie) to be printed (if to screen).
              KE=K+(44/irows)   ! set for 248
              KE4=K4+(44/irows) ! set for 800
              ie=is+9
              if(KE.le.248)then
                
C If writing to file remove blanks from label (up to 28 char with
C a bit more for the units) lets assume average is 24 char which
C says that ~33 labels can be written in the 800 char width. If data
C relates to surfaces then labels take up more space. If there is
C a delimeter being used in the data columns also apply this in
C the generation of the heading of text file.
                if (ixopen.eq.1) then
                  call SDELIM(TABLABEL(IL),t28,'N',IW)
                  if(delim.eq.'-'.or.delim.eq.'S')then
                    write(outs248(K:KE),'(3a)') ' ',
     &                t28(1:lnblnk(t28)),unit
                  elseif(delim.eq.'C')then
                    write(outs248(K:KE),'(3a)') ',',
     &                t28(1:lnblnk(t28)),unit
                  elseif(delim.eq.'T')then
                    write(outs248(K:KE),'(3a)') CHAR(9),
     &                t28(1:lnblnk(t28)),unit
                  endif
                  K=K+lnblnk(t28)+lnblnk(unit)+1
                else
                  if (ir.lt.4) then
                    write (outs248(K:KE),'(2a)') '|',TABLABEL(IL)(is:ie)
                  else
                    write (outs248(K:KE),'(2a)') '| ',unit
                  endif
                  K=KE
                endif
              endif

C For writing to file also fill the 800 char buffer.
              if(KE4.le.800)then
                if (ixopen.eq.1) then
                  call SDELIM(TABLABEL(IL),t28,'N',IW)
                  if(delim.eq.'-'.or.delim.eq.'S')then
                    write(outs800(K4:KE4),'(3a)') ' ',
     &                t28(1:lnblnk(t28)),unit
                  elseif(delim.eq.'C')then
                    write(outs800(K4:KE4),'(3a)') ',',
     &                t28(1:lnblnk(t28)),unit
                  elseif(delim.eq.'T')then
                    write(outs800(K4:KE4),'(3a)') CHAR(9),
     &                t28(1:lnblnk(t28)),unit
                  endif
                  K4=K4+lnblnk(t28)+lnblnk(unit)+1
                else
                  if (ir.lt.4) then
                    write (outs800(K4:KE4),'(2a)') '|',
     &                TABLABEL(IL)(is:ie)
                  else
                    write (outs800(K4:KE4),'(2a)') '| ',unit
                  endif
                  K4=KE4
                endif
              endif
 20         continue
            is=ie+1

C Print titles on first day and only on subsequent ones if day
C demarcations are omitted.
C Debug.
C            write(6,*) outs248(1:lnblnk(outs248))
C            write(6,*) ' '
C            write(6,*) outs800(1:lnblnk(outs800))
C            write(6,*) ' '

            lnb=lnblnk(outs248)+1   ! ensure we do not warp line
            if (IDAY.eq.IODS)then
              if(ixopen.eq.1)then
                lnb=lnblnk(outs800)   ! write directly to file
                write(itru,'(A)',iostat=ios,err=1)outs800(1:lnb)
              else
                lnb=MIN0(lnblnk(outs248),124)
                if(lnb.lt.124)then
                  call edisp(itru,outs248(1:lnb))
                else
                  call edisp248(itru,outs248,124)
                endif
              endif

            else
              if (IDHFLG.eq.1.and.ixopen.eq.1)then
                lnb=lnblnk(outs800)   ! write directly to file
                write(itru,'(A)',iostat=ios,err=1)outs800(1:lnb)
              elseif (IDHFLG.eq.1.and.ixopen.eq.0)then
                lnb=MIN0(lnblnk(outs248),124)
                if(lnb.lt.124)then
                  call edisp(itru,outs248(1:lnb))
                else
                  call edisp248(itru,outs248,124)
                endif
              else
                continue
              endif
            endif
 100      continue
        endif

C Recover results for day.
        call GOGET(IDAY)

C Loop through day's timesteps.
        DO 421 J = 1,NDTS,NOUT

C Compute current time.
C IHRD - number of days since start of plotting period in hours.
C TIME - time in hours since start of first day plotted.
          IHRD=(IDAY-IODS)*24
          call DATIME(J,ATIME)
          TIME=float(IHRD)+ATIME

C Within requested output period.
          IF(TIME.LT.(TSTART-1.0).or.TIME.GT.TFINSH)goto 421

C Display data. Create both data strings for this timestep.
          outs248='  '
          outs800='  '
          if (IHFLAG.eq.0) then 
            if(ipvform.eq.3)then
              call SJTIME(J,LJTIME)
              write (outs248,'(3a)') sq,LJTIME,sq
              write (outs800,'(3a)') sq,LJTIME,sq
              K=11
            else
              call STIME(J,LTIME)
              write (outs248,'(a5)') LTIME
              write (outs800,'(a5)') LTIME
              K=6
            endif
          elseif (IHFLAG.eq.1) then
            if(ipvform.eq.3)then
              call SJTIME(J,LJTIME)
              write (outs248,'(3a)') sq,LJTIME,sq
              write (outs800,'(3a)') sq,LJTIME,sq
              K=11
            else
              RDOTY=REAL(IDAY)+(ATIME/24.)
              write (outs248,'(f11.6)') RDOTY
              write (outs800,'(f11.6)') RDOTY
              K=11
            endif
          elseif (IHFLAG.eq.2) then

C Write in format that spreadsheets understand as time.
            call edayr(IDAY,ID,IM)
            if(ipvform.eq.3)then
              call SJTIME(J,LJTIME)
              write (outs248,'(a,i4,a,i2.2,a,i2.2,3a)') 
     &          sq,IYEAR,'-',IM,'-',ID,' ',LJTIME,sq
              write (outs800,'(a,i4,a,i2.2,a,i2.2,3a)') 
     &          sq,IYEAR,'-',IM,'-',ID,' ',LJTIME,sq
              K=22
            else
              call SJTIME(J,LJTIME)
              write (outs248,'(i4,a,i2.2,a,i2.2,2a)') 
     &          IYEAR,'-',IM,'-',ID,' ',LJTIME
              write (outs800,'(i4,a,i2.2,a,i2.2,2a)') 
     &          IYEAR,'-',IM,'-',ID,' ',LJTIME
              K=20
            endif
          endif

C First write data to the 248 char string buffer.
          klast=k
          do 410 IG=1,NGET
            KE=K+11
            if(KE.le.248)then
              IZONE=IGETNO(IG,2)

C If there is occupancy filter and occupancy then include in check.
C Assume fully occupied.
              ih=int(ATIME+1.)
              ioc=1
              if(iocupf.eq.1) call getocup(IZONE,IDAY,J,ioc,ier)
              if(ioc.ne.0) then
                write (outs248(K:KE),'(f11.2)') VAL2(IG,J)
              elseif (IGETNO(IG,1).ge.50.AND.IGETNO(IG,1).le.59) then
                if (VAL2(IG,J).gt.100.0) then
                  write (outs248(K:KE),'(a)') '  invl dT  '
                  IINLVDT=1
                else
                  write (outs248(K:KE),'(f11.2)') VAL2(IG,J)
                endif
              else
                write (outs248(K:KE),'(a)') '  not occ  '
              endif
              K=K+11
            endif
 410      continue

C If writing to file then write data to the 800 char string buffer.
          if(ixopen.eq.1)then
            k=klast   ! re-establish k position value
            do 411 IG=1,NGET
              KE=K+11
              if(KE.le.800)then
                IZONE=IGETNO(IG,2)

C If there is occupancy filter and occupancy then include in check.
C Assume fully occupied.
                ih=int(ATIME+1.)
                ioc=1
                if(iocupf.eq.1) call getocup(IZONE,IDAY,J,ioc,ier)
                if(ioc.ne.0) then
                  write (outs800(K:KE),'(f11.2)') VAL2(IG,J)
                elseif (IGETNO(IG,1).ge.50.AND.IGETNO(IG,1).le.59) then
                  if (VAL2(IG,J).gt.100.0) then
                    write (outs800(K:KE),'(a)') '  invl dT  '
                    IINLVDT=1
                  else
                    write (outs800(K:KE),'(f11.2)') VAL2(IG,J)
                  endif
                else
                  write (outs800(K:KE),'(a)') '  not occ  '
                endif
                K=K+11
              endif
 411        continue
          endif

          if(ixopen.eq.1)then
            lnb=lnblnk(outs800)   ! write directly to file

            if(delim.eq.'-')then
              write(itru,'(A)',iostat=ios,err=1)outs800(1:lnb)
            else

C If delimiter set to alternative then process text before writing.
C If using X delimeter (tagged data) then set the delimeter to a comma.
C If IHFLAG is 2 then the initial 19 or 21 characters will be a time
C stamp that needs the 11th character to be a literal space.
              dg=delim
              if (delim.eq.'X') dg='C'
              call SDELIM(outs800,outs800d,dg,IW)
              if(IHFLAG.eq.2) then
                write(outs800d(11:11),'(1a)') ' '
              endif
              lnb=lnblnk(outs800d)   ! write directly to file
              write(itru,'(A)',iostat=ios,err=1) outs800d(1:lnb)
            endif
          else

C Writing to screen so use shorter file buffer.
            lnb=MIN0(lnblnk(outs248),124)
            if(lnb.lt.124)then
              call eddisp(itru,outs248(1:lnb))
            else
              call eddisp248(itru,outs248,124)
            endif
          endif

 421    CONTINUE
 10   continue

C Explain inlv dT comment.
      if (IINLVDT.eq.1) then
        call edisp(itru,'invl dT: the supplied temperature difference')
        call edisp(itru,' to the comfort algorithm was outside the ')
        call edisp(itru,' valid range, thus no data could be returned.')
      endif

C End tag for tagged data
      if (delim.eq.'X') then
        write(outs,'(a)')'*end_table'
        call edisp(itru,outs)
      endif
      
C Allow other/ re- display?
      if(act(1:1).eq.'p'.or.act(1:1).eq.'m')then
        return
      else
        goto 1
      endif

      END

C ******************** MGLTBL ********************
C MGLTBL: monthly multi-column tabular listing for selected
C gains and losses.
      SUBROUTINE MGLTBL
#include "building.h"
#include "geometry.h"
#include "schedule.h"
      
      integer lnblnk  ! function definition

C      COMMON/SPAD/MMOD,LIMIT,LIMTTY
      COMMON/FILEP/IFIL
      COMMON/OUTPCH/ICOUT
      COMMON/SIMPIK/ISIM,ISTADD,ID1,IM1,ID2,IM2,ISDS,ISDF,NTS,ISAVE
      common/recver/izver,ipver,iever
      COMMON/ZONPIK/NZ,NZNO(MCOM)
      COMMON/PERO/IOD1,IOM1,IOH1,IOD2,IOM2,IOH2,IODS,IODF,NOUT,IAV
      COMMON/GETPIK/NGET,IGETNO(MZS,9)
      COMMON/GET1/VAL1(MZS,MTS),VAL2(MZS,MTS),VAL3(MZRL,MTS)

      character SLABEL*32,GLABEL*20,TABLABEL*36
      COMMON/GETLABEL/SLABEL(MZS),GLABEL(MZS),TABLABEL(MZS)
      integer LNSLABEL,LNGLABEL,LNTABLABEL  ! lengths for label strings
      COMMON/LNGETLABEL/LNSLABEL(MZS),LNGLABEL(MZS),LNTABLABEL(MZS)
      common/getmenu/menutype,igetind(65),igetflux(65)

      COMMON/GET2/XDUM(MTS),XDUM1(MTS),GVAL(MTS)
      COMMON/EXPORTI/ixopen,ixunit,ixpunit
      COMMON/RESLIB/RFILE,PFILE,MSTRFILE,LAFRES
      COMMON/SETNAM/RSNAME(MNRS)
      COMMON/SIMPKA/NSIM
      integer ncomp,ncon
      COMMON/C1/NCOMP,NCON

      CHARACTER PDESCR*60,SDESCR*44,CSTR*25,NMTHNM(12)*3
      character outs*124,prompt*124,outsl*144
      CHARACTER RSNAME*40
      character rfile*72,PFILE*72,MSTRFILE*72,LAFRES*72

      DIMENSION VAL(15),TVAL(15,12)
      DIMENSION NDYMTH(12), CQ(MTS), CQOUT(MTS)
      DIMENSION CASDAY(MTS,4)  ! to hold casual type data
      integer looplimit
      real biggest  ! guage size of annual numbers
      real perocupc,perocupr,perocupl ! average occupant to write out
      real perlightc,perlightr,perlightl ! average lighting to write out
      real perequipc,perequipr,perequipl ! average equipment to write out
      real otherc,otherr,otherl ! average other (future expansion) to write out
      integer theonectld  ! if non-zero the casual gain type that is controlled.
      logical XST,dmdsok  ! for dispersed demands
      real overallm2,zonem2frac(MCOM)  ! total of all the selected zones base areas

      DATA NDYMTH/31,59,90,120,151,181,212,243,273,304,334,365/
      DATA NMTHNM/'Jan','Feb','Mar','Apr','May','Jun','Jul',
     &'Aug','Sep','Oct','Nov','Dec'/

C Set chosen result set.
      ISET=ISIM
      IEND=24*NTS

C Read project demands file if it exists.
      IUO=IFIL+1
      call FINDFIL(bdmds,XST)
      IF(XST)THEN
        CALL ERPFREE(IUO,ISTAT)
        CALL ERBDMD(0,IUO,IER)
        dmdsok=.true.
      else
        dmdsok=.false.
      endif

C Generate descriptive strings & output general header information.
C If output to file alter the edisp unit number.
      prompt = ' '
      itru = icout
      if(ixopen.eq.1)then
        itru = ixunit
      endif

C Call the menu of choices (this also sets some default options).
      MENUTYPE=13
      call GOMSETUP
      call GOMENU
      if(ixopen.eq.1)then
        itru = ixunit
        if(NGET.ge.1)then
          write(prompt,'(a,a)') SLABEL(1)(1:LNSLABEL(1)),' >> file.'
        else
          write(prompt,'(a)')' Output being directed to file. ' 
        endif
        call usrmsg(prompt,'Scanning for range of values...','-')
      else
        write(prompt,'(3a)') 'Scanning for ',
     &    SLABEL(1)(1:LNSLABEL(1)),'...'
        itru = icout
        call usrmsg(prompt,'  ','-')
      endif

C Recover 100Wh vs kWh from IGETNO().
      IR=IGETNO(1,4)

C Set header text.
      CALL HDDATE(PDESCR)
      CALL HDSTEP(SDESCR)
      call edisp(itru,' ')
      lsn1=MIN0(lnblnk(RFILE),42)
      if(NSIM.gt.1)then
        WRITE(outs,'(3A,I4,2A)')'Lib: ',RFILE(1:lsn1),' Set:',ISIM,
     &    ': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
      else
        WRITE(outs,'(4A)')'Lib: ',RFILE(1:lsn1),
     &    ': ',RSNAME(ISIM)(1:lnblnk(RSNAME(ISIM)))
      endif
      call edisp(itru,outs)
      WRITE(outs,'(3A)')PDESCR(1:lnblnk(PDESCR)),' ',
     &   SDESCR(11:lnblnk(SDESCR))
      call edisp(itru,outs)
      call edisp(itru,' ')

C Echo header with units.
      if(IR.eq.1)call edisp(itru,
     &' Monthly selection of gains & losses (to nearest 100Wh).')
      if(IR.eq.2)call edisp(itru,
     &' Monthly selection of gains & losses (to nearest kWh).')
      call edisp(itru,' ')

C If current version display monthly summary.
      if(izver.ge.4)then

C Monthly statistics of energy gains and losses.
        if(dmdsok)then
          write(outsl,'(3a)')
     &    ' Zone      Period| Surface convection at face of |  Casual',
     &    ' Gains (Radiant+Conv)  |Air movement   | Environmental ',
     &    '|Solar radiation |dispersed'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '           in    |   transparent |    opaque     |Occu-  |',
     &    'Lights |Small  |Other  |Infil- |Venti- |   controls    ',
     &    '|absorbd|entering| demands'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '           month |outside|other  |outside|other  |pant   |',
     &    '       |Power  |       |tration|lation |Heating|Cooling',
     &    '|within |the     |for model'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '                 |facing |facing |facing |facing |       |',
     &    '       |       |       |       |       |       |       ',
     &    '|zone   |zone    |'
          call edisp(itru,outsl)
        else
          write(outsl,'(3a)')
     &    ' Zone      Period| Surface convection at face of | Casual',
     &    ' Gains (Radiant+Conv)   |Air movement   | Environmental ',
     &    '|Solar radiation |'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '           in    |   transparent |    opaque     |Occu-  ',
     &    '|Lights |Small  |Other  |Infil- |Venti- |   controls    ',
     &    '|absorbd|entering|'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '           month |outside|other  |outside|other  |pant   ',
     &    '|       |Power  |       |tration|lation |Heating|Cooling',
     &    '|within |the     |'
          call edisp(itru,outsl)
          write(outsl,'(3a)')
     &    '                 |facing |facing |facing |facing |       ',
     &    '|       |       |       |       |       |       |       ',
     &    '|zone   |zone    |'
          call edisp(itru,outsl)
        endif
      endif

C For each zone in model sum the zone base areas and then get the
C fraction of model base area for the zone.
      overallm2=0.0
      DO izn=1,NCOMP
        overallm2=overallm2+ZBASEA(izn)
      ENDDO
      DO izn=1,NCOMP
        zonem2frac(izn)=ZBASEA(izn)/overallm2
      ENDDO
      write(outs,*) 'The model overall base area is ',overallm2,'m2.'
      call edisp(itru,outs)

C Loop for 13 topics and then each month.
      DO 620 IJK=1,15
        DO 610 IMNTH=1,12
          TVAL(IJK,IMNTH)=0.0
  610   CONTINUE
  620 CONTINUE

C Per zone statistics.
      DO 660 KGET=1,NGET
        izn=IGETNO(KGET,2)
        DO 621 IJK=1,15
          VAL(IJK)=0.0
  621   CONTINUE

C Get statistics for each day.
        IMNTH=IOM1
        DO 650 J=IODS,IODF
          JD=J
          NEL=0
          NN=NZSUR(IZN)
          CALL CHKTIME(JD,ISTART,IEND)

C For each of the items perform a low level get for the days data,
C filter it according to IAV and get the sums for the day.

C Column 1 tmc external, 2 tmc internal, 3 opq external, 4 opq internal.
C If version 2 of library then recover via goutopq etc.
          if(ISAVE.lt.4)then
            CALL GOUTOPQ(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            do ISTP=ISTART,IEND
              VAL(3)=VAL(3)+CQ(ISTP)
            enddo
            CALL GOPQIN(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            do ISTP=ISTART,IEND
              VAL(4)=VAL(4)+CQ(ISTP)
            enddo
            CALL GOUTTRN(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            do ISTP=ISTART,IEND
              VAL(1)=VAL(1)+CQ(ISTP)
            enddo
            CALL GTRNIN(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            do ISTP=ISTART,IEND
              VAL(2)=VAL(2)+CQ(ISTP)
            enddo
          elseif(ISAVE.eq.4)then

C Surface convection contributions, loop through each surface,
C find if interior or exterior etc. Note convection is calculated
C in terms of surface so negate it to have contribution to air node.
            DO 60 ISN= 1,NN
              CALL SURADJ(IZN,ISN,IE,T,IZC,ISC,ICN,CSTR)
              CALL CSCONV(JD,IZN,ISN,CQ)
              CALL FLTIAV(JD,CQ,CQOUT,NEL)
              DO 61 ISTP=ISTART,IEND
                IF(IE.EQ.0)THEN
                  if(SSOTF(ICN)(1:4).EQ.'OPAQ')then
                    VAL(3)=VAL(3)+CQOUT(ISTP)*(-1.0)
                  else
                    VAL(1)=VAL(1)+CQOUT(ISTP)*(-1.0)
                  endif
                ELSE
                  if(SSOTF(ICN)(1:4).EQ.'OPAQ')then
                    VAL(4)=VAL(4)+CQOUT(ISTP)*(-1.0)
                  else
                    VAL(2)=VAL(2)+CQOUT(ISTP)*(-1.0)
                  endif
                ENDIF
   61         CONTINUE
   60       CONTINUE
          else
            call usrmsg('Library version or save level doesn`t support',
     &        'recovery of convection data.','W')
          endif

          if(izver.ge.4)then

C Get seperate casual gain total rad+convec. NOTE: this has been tested
C for use with zone operations - but not yet temporal.

C << extend code to check for TDF and NCM data structures >>

            DO J2=ISTART,IEND
              call getallcas(JD,IZN,ISET,J2,QCASR,QCASC,QCASL,FRAC,
     &          perocupc,perlightc,perequipc,perotherc,perocupr,
     &          perlightr,perequipr,perotherr,perocupl,perlightl,
     &          perequipl,perotherl,theonectld)
              CASDAY(J2,1)=perocupc+perocupr ! Total occupant gain (W)
              CASDAY(J2,2)=perlightc+perlightr ! Total lighting gain (W)
              CASDAY(J2,3)=perequipc+perequipr ! Total small power gain (W)
              CASDAY(J2,4)=perotherc+perotherr ! Total other gain (W)
            ENDDO

C Pass across the occupant gains into gval and then filter and sum.
            DO J2=ISTART,IEND
              GVAL(J2)=CASDAY(J2,1)
            ENDDO
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(5)=VAL(5)+SGT+SLT

C Pass across the lighiting gains into gval and then filter and sum.
            DO J2=ISTART,IEND
              GVAL(J2)=CASDAY(J2,2)
            ENDDO
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(6)=VAL(6)+SGT+SLT

C Pass across the small power gains into gval and then filter and sum.
            DO J2=ISTART,IEND
              GVAL(J2)=CASDAY(J2,3)
            ENDDO
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(7)=VAL(7)+SGT+SLT

C Pass across the other slot gains into gval and then filter and sum.
            DO J2=ISTART,IEND
              GVAL(J2)=CASDAY(J2,4)
            ENDDO
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(8)=VAL(8)+SGT+SLT

C Infiltration.
            CALL GQV1(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(9)=VAL(9)+SGT+SLT

C Ventilation.
            CALL GQV2(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(10)=VAL(10)+SGT+SLT

C Convection portion of plant.
            CALL GZQM(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(11)=VAL(11)+SGT
            VAL(12)=VAL(12)+SLT

C Solar absorbed in the zone.
            call GSOLABS(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(13)=VAL(13)+SGT

C Solar entering zone from outside.
            call GSOLX(JD,IZN,ISET)
            CALL FLTIAV(JD,GVAL,CQ,NEL)
            CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
            VAL(14)=VAL(14)+SGT

C If dispersed demands (via VAL3 arrays) sum them to VAL(15).
C As dispersed demands are for whole model in the reporting
C for each zone factor it by the base area of that zone.
            if(dmdsok)then
              call DDMDS(JD)
              DO J2=ISTART,IEND
                GVAL(J2)=VAL3(15,J2)+VAL3(16,J2)+VAL3(17,J2)+
     &            VAL3(18,J2)+VAL3(19,J2)+VAL3(20,J2)+VAL3(21,J2)
              ENDDO
              CALL FLTIAV(JD,GVAL,CQ,NEL)
              CALL SUM1V(CQ,ISTART,IEND,SGT,SLT,IGT,IEQ,ILT)
              VAL(15)=VAL(15)+(SGT*zonem2frac(izn))
            else
              VAL(15)=0.0
            endif

          endif

C Check if month finished.
          IF(JD.LT.NDYMTH(IMNTH).AND.JD.NE.IODF)goto 650

C Dump this month's statistics.
          DO IJK=1,15
            VAL(IJK)=VAL(IJK)/1000.
          ENDDO

          if(dmdsok)then
            looplimit=15
          else
            looplimit=14
          endif
          IF(IMNTH.EQ.IOM1)THEN
            if(IR.eq.1)then
              WRITE(outsl,701)zname(IZN)(1:12),NMTHNM(IMNTH),
     &                      (VAL(IJ),IJ=1,looplimit)
  701         FORMAT(1X,A12,1X,A3,1X,15F8.1)
            elseif(IR.eq.2)then
              WRITE(outsl,703)zname(IZN)(1:12),NMTHNM(IMNTH),
     &                      (VAL(IJ),IJ=1,looplimit)
  703         FORMAT(1X,A12,1X,A3,1X,15F8.0)
            endif
            call eddisp(itru,outsl)
          ELSE
            if(IR.eq.1)then
              WRITE(outsl,702)NMTHNM(IMNTH),(VAL(IJ),IJ=1,looplimit)
  702         FORMAT(14x,A3,1X,15F8.1)
            elseif(IR.eq.2)then
              WRITE(outsl,704)NMTHNM(IMNTH),(VAL(IJ),IJ=1,looplimit)
  704         FORMAT(14x,A3,1X,15F8.0)
            endif
            call eddisp(itru,outsl)
          ENDIF

          DO IJK=1,looplimit
            TVAL(IJK,IMNTH)=TVAL(IJK,IMNTH)+VAL(IJK)
            VAL(IJK)=0.0
          ENDDO
          IMNTH=IMNTH+1
  650   CONTINUE
  660 CONTINUE

C Dump 'all-zone' statistics if more than one zone.
      if(dmdsok)then
        looplimit=15
      else
        looplimit=14
      endif
      DO 680 IMNTH=IOM1,IOM2
        if (NZ.GT.1) then
          IF(IMNTH.EQ.IOM1)THEN
            write(outsl,705)NMTHNM(IMNTH),
     &        (TVAL(IJK,IMNTH),IJK=1,looplimit)
  705       format(' All zones    ',A3,1X,15(F8.0))
            call edisp(itru,' ')
            call eddisp(itru,outsl)
          ELSE
            write(outsl,706)NMTHNM(IMNTH),
     &        (TVAL(IJK,IMNTH),IJK=1,looplimit)
  706       format(14X,A3,1X,15(F8.0))
            call eddisp(itru,outsl)
          ENDIF
        endif
        biggest=0.0
        DO IJK=1,looplimit
          VAL(IJK)=VAL(IJK)+TVAL(IJK,IMNTH)
          if(VAL(IJK).gt.biggest) biggest=VAL(IJK)
        ENDDO
  680 CONTINUE

C Dump the annual totals if run was for more than one month.

C Find if any of the annual totals is large enough to require 10 digits
C otherwise write with 8 or 9 digits.
      if(dmdsok)then
        looplimit=15
      else
        looplimit=14
      endif
      if (IOM2.GT.IOM1) then
        if(biggest.gt.999999.0)then
          write(outsl,'(A,15(F10.0))') ' Annual   ',
     &      (VAL(IJK),IJK=1,looplimit)
        elseif(biggest.gt.99999.0.and.biggest.lt.999999.0)then
          write(outsl,'(A,15(F9.0))')' Annual      ',
     &      (VAL(IJK),IJK=1,looplimit)
        else
          write(outsl,'(A,15(F8.0))')' Annual           ',
     &      (VAL(IJK),IJK=1,looplimit)
        endif
        call edisp(itru,' ')
        call eddisp(itru,outsl)
      endif
      call edisp(itru,' ')
      if(ixopen.eq.1)then
        call usrmsg(prompt,'Scanning for range of values...done.','-')
      else
        call usrmsg('  ','  ','-')
      endif

      RETURN
      END


C ******************** CZMRT ********************
C CZMRT is a mid level routine to return one days temperatures
C of zone mean radiant temperature. Returns value via XDUM1 
C in common block GET2.
C CALLS: GZTMS.

      SUBROUTINE CZMRT(IDAY,IZONE,ISET)
#include "building.h"
#include "geometry.h"
      COMMON/OUTIN/IUOUT,IUIN
      COMMON/SIMPIK/ISIM,ISTADD,ID1,IM1,ID2,IM2,ISDS,ISDF,NTS,ISAVE
      COMMON/GET2/XDUM(MTS),XDUM1(MTS),GVAL(MTS)

      DIMENSION ZTMS(MS),YDUM(MS),TSO(MS)

      N=24*NTS
      IF(ISAVE.GT.1.AND.ISAVE.LE.4)THEN
        DO 74,JC1=1,N
          XDUM1(JC1)=0.0
          IS=ISET
          CALL GZTMS(IDAY,IZONE,IS,JC1,ZTMS,TSO,TAIR,TMCON,A1,A2,A5,RH)
          YDUM(1)=A1
          YDUM(2)=A2
          YDUM(3)=0.
          YDUM(4)=0.
          YDUM(5)=A5
          CALL MOMNRD(IZONE,TAIR,ZTMS,YDUM,NZSUR(IZONE),TMNRD)
          XDUM1(JC1)=TMNRD
   74   CONTINUE
      ELSE
        call edisp(iuout,' CZMRT: Save option does not allow access.')
      ENDIF
      RETURN
      END

C ******************** CZRESL ********************

C CZRESL is a mid level routine to return one day's values for
C zone resultant temperature.  Returns value via XDUM1 in common GET2.
C CALLS:  GZTMS.

      SUBROUTINE CZRESL(IDAY,IZONE,ISET)
#include "building.h"
#include "geometry.h"
      COMMON/OUTIN/IUOUT,IUIN
      COMMON/SIMPIK/ISIM,ISTADD,ID1,IM1,ID2,IM2,ISDS,ISDF,NTS,ISAVE
      COMMON/GET2/XDUM(MTS),XDUM1(MTS),GVAL(MTS)
      DIMENSION ZTMS(MS),YDUM(MS),TSO(MS)

      N=24*NTS

      IF(ISAVE.GT.1.AND.ISAVE.LE.4)THEN
        DO 74,JC1=1,N
          XDUM1(JC1)=0.0
          IS=ISET
          CALL GZTMS(IDAY,IZONE,IS,JC1,ZTMS,TSO,TAIR,TMCON,A1,A2,A5,RH)
          YDUM(1)=A1
          YDUM(2)=A2
          YDUM(3)=0.
          YDUM(4)=0.
          YDUM(5)=A5
          CALL MORESL(IZONE,TAIR,ZTMS,YDUM,NZSUR(IZONE),TRESL)
          XDUM1(JC1)=TRESL
   74   CONTINUE
      ELSE
        call edisp(iuout,' CZRESL: Save option does not allow access.')
      ENDIF
      RETURN
      END

