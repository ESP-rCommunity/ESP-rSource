      SUBROUTINE SELECT(IFILE,NMIN,NMAX,IND,IVAL)
C
C   READS AN INPUT LINE from file IFILE
C   SPECIFYING NUMBERS IN RANGE NMIN-NMAX
C   AND SETS IND(I)=IVAL FOR EACH NUMBER SPECIFIED
C   INPUT LINE IS OF FORM E.G.
C          2,3,5-9,10
C   OR     1-6  9-12, 16
C   OR     ALL
C   I.E. SEPARATORS ARE SPACE OR COMMA
C        RANGE IS N-M (5-9 MEANS 5,6,7,8,9)
C
      COMMON/OUTIN/IUOUT,IUIN,IEOUT
      DIMENSION IND(0:*)
      CHARACTER*72 LINE
      CHARACTER*1  CHAR
C
C   GET THE LINE
  10  READ(ifile,1000,ERR=900)LINE
 1000 FORMAT(A72)
C
      IF( LINE(1:3).EQ.'ALL' .or. LINE(1:3).EQ. 'all')GO TO 600
      numin=0
      NUM=0
      MINUS=0
      NUM1=0
C
      DO 100 I=1,72
      CHAR=LINE(I:I)
C
      IF(CHAR.EQ.' ')GO TO 300
      IF(CHAR.EQ.',')GO TO 300
      IF(CHAR.EQ.'-')GO TO 500
C   GET NUMERIC? VALUE
      NVAL=ICHAR(CHAR)-48
      IF(NVAL.GE.0  .AND. NVAL.LE.9)GO TO 200
C   OTHERWISE ERROR
      GO TO 900
C
C   NUMERIC DIGIT
 200  NUM=NUM*10+NVAL
      numin=1
      GO TO 100
C
C   SPACE OR COMMA
C   IGNORE IF NO NUMBER YET
 300  IF(NUMIN.EQ.0 )GO TO 100
C   IF NUMBER OUT OF RANGE, ERROR
      IF(NUM.GT.NMAX)GO TO 920
      IF(NUM.LT.NMIN)GO TO 920
C   IF RANGE SET
      IF(MINUS.EQ.1)GO TO 400
C   SET IND
      IND(NUM)=IVAL
C   SET POSSIBLE START OF RANGE
      NUM1=NUM
C   RESET FOR NEXT
      NUM=0
      numin=0
      GO TO 100
C
C   RANGE
 400  IF(NUM.EQ.0 )GO TO 100
      DO 450 J=NUM1,NUM
      IND(J)=IVAL
 450   CONTINUE
      NUM=0
      NUM1=0
      MINUS=0
      numin=0
      GO TO 100
C
C  SET RANGE ON
 500  MINUS=1
      NUM1=NUM
      NUM=0
      numin=0
      GO TO 100
C
C  GET NEXT CHAR
 100  CONTINUE
      RETURN
C
C  SET ALL
 600  DO 650 J=NMIN,NMAX
      IND(J)=IVAL
 650  CONTINUE
      RETURN
C
C   ERROR
 900  WRITE(IUOUT,1100)
 1100 FORMAT('+SELECT ERROR, Respecify>')
      GO TO 10
 920  WRITE(IUOUT,1200)
 1200 FORMAT('+SELECT:Number out of range, Respecify>')
      GO TO 10
C
      END
